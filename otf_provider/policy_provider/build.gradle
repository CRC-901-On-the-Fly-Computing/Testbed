
// define PoC properties for this project
// for more information, see root projects `build.gradle`
ext.poc = ext {
	projectName            = 'otfp_policy_provider'
	basePackage            = 'de.upb.crc901.otftestbed.policy_provider'
}

// configure this project according to PoC properties
configureProject()


apply plugin: 'org.springframework.boot'


springBoot {
	mainClassName = "${basePackage}.impl.Application"
}

task run(type: JavaExec) {
	main      = "${basePackage}.impl.Application"
	classpath = sourceSets.main.runtimeClasspath
}

jar {
	archiveBaseName.set((String) poc.projectName)
	archiveVersion.set( (String) poc.projectVersion)
}

bootJar {
	archiveBaseName = jar.archiveBaseName
	archiveVersion  = jar.archiveVersion
}


dependencies {
	// project dependencies
	implementation project(':commons')
	implementation project(':commons_connect')
	implementation project(getGenerationSubprojectPath('spring_server'))

	// SFB dependencies
	implementation group: 'de.upb.crypto.react'       , name: 'acs'

	// external dependencies
	implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind'
	implementation group: 'io.springfox'              , name: 'springfox-swagger2'
	implementation group: 'io.swagger'                , name: 'swagger-annotations'
	implementation group: 'org.apache.tomcat.embed'   , name: 'tomcat-embed-core'
	implementation group: 'org.springframework'       , name: 'spring-beans'
	implementation group: 'org.springframework'       , name: 'spring-context'
	implementation group: 'org.springframework'       , name: 'spring-core'
	implementation group: 'org.springframework'       , name: 'spring-web'
	implementation group: 'org.springframework.boot'  , name: 'spring-boot'
	implementation group: 'org.springframework.boot'  , name: 'spring-boot-autoconfigure'


	// runtime dependencies
	runtimeOnly    group: 'commons-lang'              , name: 'commons-lang'
	runtimeOnly    group: 'io.springfox'              , name: 'springfox-swagger2'
	runtimeOnly    group: 'io.springfox'              , name: 'springfox-swagger-ui'
	runtimeOnly    group: 'org.springframework.boot'  , name: 'spring-boot-starter-web'
}

project(getGenerationSubprojectPath('java_client')) {
	dependencies.api group: 'de.upb.crypto.react', name: 'acs'
}

project(getGenerationSubprojectPath('spring_server')) {
	dependencies.api group: 'de.upb.crypto.react', name: 'acs'
}
